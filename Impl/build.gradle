plugins {
    id "com.github.johnrengelman.shadow"
}

archivesBaseName = 'Hyrame-Impl'

jar {
    manifest {
        attributes 'Specific-Version': project.version
        attributes 'Implementation-Version': "${ project.version }-${ commitSha }-${ buildNumber }"
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
}

dependencies {
    compileOnly 'fr.hyriode:spigot-impl:1.8.8-SNAPSHOT'

    implementation project.parent
    shadow (project.parent) {
        exclude module: 'hyriapi'
        exclude module: 'spigot-api'
    }
    shadow 'org.reflections:reflections:0.10.2'
}

processResources {
    filter {
        it.replace('@version@', "${ project.version }-${ commitSha }-${ buildNumber }")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                groupId = project.group
                artifactId = 'hyrame-impl'
                version = project.version
                name = 'Hyrame-Impl'
                description = 'Framework to create games and plugins easily. (Impl)'
                url = 'https://github.com/Hyriode/Hyrame'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('GITHUB_USERNAME')
                password System.getenv('GITHUB_TOKEN')
            }
            name = "Hyrame_maven"
            url = "https://maven.pkg.github.com/Hyriode/Hyrame"
        }
    }
}
