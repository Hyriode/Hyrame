plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'fr.hyriode'
    version = '1.3.7'

    java {
        withSourcesJar()
        withJavadocJar()

        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()

        maven {
            name = 'HyriAPI_maven'
            url = 'https://maven.pkg.github.com/Hyriode/HyriAPI/'

            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
        }

        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }

        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }

}

archivesBaseName = 'Hyrame'

dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'libs')
    api 'fr.hyriode:hyriapi:1.4.3'
    api 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    api 'xyz.xenondevs:particle:1.8.1'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                groupId = project.group
                artifactId = 'hyrame'
                version = project.version
                name = 'Hyrame'
                description = 'Framework to create games and plugins easily.'
                url = 'https://github.com/Hyriode/Hyrame'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name = "Hyrame_maven"
            url = "https://maven.pkg.github.com/Hyriode/Hyrame"
        }
    }
}