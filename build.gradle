import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

group = 'fr.hyriode'
version = '1.1.1'
archivesBaseName = 'Hyrame'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

configurations {
    shade
    implementation.extendsFrom shade
}

repositories {
    mavenCentral()

    maven {
        name = 'HyriAPI_maven'
        url = 'https://maven.pkg.github.com/Hyriode/HyriAPI/'

        credentials {
            username System.getenv('USERNAME')
            password System.getenv('TOKEN')
        }
    }

    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    exclude 'fr/hyriode/hyrame/impl'
    exclude 'plugin.yml'
    classifier ''
}

task implJar(type: ShadowJar) {
    archiveBaseName.set('Hyrame-Impl')
    from sourceSets.main.output
    configurations = [project.configurations.shadow]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        maven(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                groupId = project.group
                artifactId = 'hyrame'
                version = project.version
                name = 'Hyrame'
                description = 'Project to create game easily'
                url = 'https://github.com/Hyriode/Hyrame'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name = "Hyrame_maven"
            url = "https://maven.pkg.github.com/Hyriode/Hyrame"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    shadow 'com.google.guava:guava:31.0.1-jre'
    shade 'fr.hyriode:hyriapi:1.0.7'
    shade 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
}

processResources {
    filter {
        it.replace('@version@', project.version)
    }
}